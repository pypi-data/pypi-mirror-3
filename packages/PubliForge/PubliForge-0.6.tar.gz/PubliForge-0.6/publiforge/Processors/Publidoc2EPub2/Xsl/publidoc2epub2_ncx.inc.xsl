<?xml version="1.0" encoding="UTF-8"?>
<!-- $Id: publidoc2epub2_ncx.inc.xsl 5b0e24cfcf19 2012/05/21 16:17:34 claire $ -->
<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:date="http://exslt.org/dates-and-times"
                extension-element-prefixes="date">

  <!--
      =========================================================================
      Template toc_ncx
      =========================================================================
  -->
  <xsl:template name="toc_ncx" >
    <xsl:document href="{$path}toc.ncx" method="xml"
                  encoding="utf-8" indent="yes"
                  doctype-public="-//NISO//DTD ncx 2005-1//EN"
                  doctype-system="http://www.daisy.org/z3986/2005/ncx-2005-1.dtd">
      <xsl:comment> Generated by PubliForge, <xsl:value-of select="date:date-time()"/> </xsl:comment>
      <ncx xmlns="http://www.daisy.org/z3986/2005/ncx/" version="2005-1">
        <xsl:attribute name="xml:lang"><xsl:value-of select="$lang"/></xsl:attribute>
        <head>
          <meta name="dtb:uid">
            <xsl:attribute name="content">
              <xsl:choose>
                <xsl:when test="$ean and $ean!='-'">
                  <xsl:value-of select="$ean"/>
                </xsl:when>
                <xsl:when test="head/identifier[@type='ean']">
                  <xsl:value-of select="head/identifier[@type='ean']"/>
                </xsl:when>
                <xsl:otherwise>
                  <xsl:choose>
                    <xsl:when test="$publisher_url"><xsl:value-of select="$publisher_url"/></xsl:when>
                    <xsl:when test="head/contributors/contributor[role='publisher']/link/@uri">
                      <xsl:value-of select="head/contributors/contributor[role='publisher']/link/@uri"/>
                    </xsl:when>
                    <xsl:otherwise>http://www.publiforge.org</xsl:otherwise>
                  </xsl:choose>
                  <xsl:value-of select="concat('/epub/', @xml:id)"/>
                </xsl:otherwise>
              </xsl:choose>
            </xsl:attribute>
          </meta>
          <meta name="dtb:totalPageCount" content="0"/>
          <meta name="dtb:maxPageNumber" content="0"/>
          <xsl:if test="$cover or head/cover">
            <meta name="cover" content="h_{translate($fid, ' ', '_')}-cover"/>
          </xsl:if>
        </head>
        <docTitle>
          <text><xsl:apply-templates select="head/title" mode="text"/></text>
        </docTitle>
        <xsl:apply-templates select="head/contributors/contributor" mode="toc_ncx"/>
        <navMap>
          <xsl:if test="$toc">
            <navPoint id="navPoint-1}" playOrder="1">
              <navLabel><text><xsl:value-of select="$i18n_toc"/></text></navLabel>
              <content src="{$fid}-toc{$html_ext}"/>
            </navPoint>
          </xsl:if>
          <xsl:apply-templates select="../topic|division|topic" mode="toc_ncx"/>
        </navMap>
      </ncx>
    </xsl:document>
  </xsl:template>

  <!--
      =========================================================================
      contributor mode toc_ncx
      =========================================================================
  -->
  <xsl:template match="contributor" mode="toc_ncx">
    <xsl:if test="role='author'">
      <docAuthor>
        <text>
          <xsl:if test="firstname">
            <xsl:apply-templates select="firstname" mode="text"/>
            <xsl:text> </xsl:text>
          </xsl:if>
          <xsl:if test="secondname">
            <xsl:apply-templates select="secondname" mode="text"/>
            <xsl:text> </xsl:text>
          </xsl:if>
          <xsl:apply-templates select="lastname|label" mode="text"/>
        </text>
      </docAuthor>
    </xsl:if>
  </xsl:template>

  <!--
      =========================================================================
      division mode toc_ncx
      =========================================================================
  -->
  <xsl:template match="division" mode="toc_ncx">
    <xsl:variable name="count">
      <xsl:value-of select="count(preceding::division)
                            + count(ancestor::division)
                            + count(preceding::topic) + $toc + 1"/>
    </xsl:variable>
    <navPoint id="navPoint-{$count}" playOrder="{$count}">
      <navLabel>
        <text>
          <xsl:choose>
            <xsl:when test="head/title">
              <xsl:apply-templates select="head/title" mode="text"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="concat($i18n_part, ' ', count(preceding::division)+1)"/>
            </xsl:otherwise>
          </xsl:choose>
          <xsl:if test="head/subtitle">
            <xsl:text> – </xsl:text>
            <xsl:apply-templates select="head/subtitle" mode="text"/>
          </xsl:if>
        </text>
      </navLabel>
      <xsl:choose>
        <xsl:when test="ancestor::division">
          <content src="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}#div{count(ancestor::division)}"/>
        </xsl:when>
        <xsl:when test="$subtoc">
          <content src="{$fid}-div-{count(preceding::division)+1}{$html_ext}"/>
        </xsl:when>
        <xsl:otherwise>
          <content src="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}"/>
        </xsl:otherwise>
      </xsl:choose>
      <xsl:apply-templates select="division|topic" mode="toc_ncx"/>
    </navPoint>
  </xsl:template>

  <!--
      =========================================================================
      topic mode toc_ncx
      =========================================================================
  -->
  <xsl:template match="topic" mode="toc_ncx">
    <xsl:variable name="count">
      <xsl:value-of select="count(preceding::division)
                            + count(ancestor::division)
                            + count(preceding::topic) + $toc + 1"/>
    </xsl:variable>
    <navPoint id="navPoint-{$count}" playOrder="{$count}">
      <navLabel>
        <text>
          <xsl:choose>
            <xsl:when test="@type='title'">
              <xsl:value-of select="$i18n_title_page"/>
            </xsl:when>
            <xsl:when test="head/title">
              <xsl:apply-templates select="head/title" mode="text"/>
            </xsl:when>
            <xsl:when test="section[1]/head/title">
              <xsl:apply-templates select="section[1]/head/title" mode="text"/>
            </xsl:when>
            <xsl:when test="section[1]/p[1]/initial">
              <xsl:apply-templates select="section[1]/p[1]/initial" mode="text"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="concat($i18n_chapter, ' ', count(preceding::topic)+1)"/>
            </xsl:otherwise>
          </xsl:choose>
          <xsl:if test="head/subtitle">
            <xsl:text> – </xsl:text>
            <xsl:apply-templates select="head/subtitle" mode="text"/>
          </xsl:if>
        </text>
      </navLabel>
      <content src="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}#top"/>
    </navPoint>
  </xsl:template>

</xsl:stylesheet>
