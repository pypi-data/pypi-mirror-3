<?xml version="1.0" encoding="UTF-8"?>
<!-- $Id: publidoc2epub_ncx.inc.xsl 632579c1ad08 2012/02/13 22:15:34 patrick $ -->
<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <!--
      ========================================================================
      Template toc_ncx
      ========================================================================
  -->
  <xsl:template name="toc_ncx" >
    <xsl:document href="{$path}toc.ncx" method="xml"
                  encoding="UTF-8" indent="yes"
		  doctype-public="-//NISO//DTD ncx 2005-1//EN"
		  doctype-system="http://www.daisy.org/z3986/2005/ncx-2005-1.dtd">
      <xsl:comment> Generated by PubliForge, $Date: 2012/02/13 22:15:34 $ </xsl:comment>
      <ncx xmlns="http://www.daisy.org/z3986/2005/ncx/" version="2005-1">
        <xsl:attribute name="xml:lang"><xsl:call-template name="lang"/></xsl:attribute>
        <head>
          <meta name="dtb:uid">
            <xsl:attribute name="content">
              <xsl:text></xsl:text>
              <xsl:choose>
                <xsl:when test="$publisher_url"><xsl:value-of select="$publisher_url"/></xsl:when>
                <xsl:when test="head/contributors/contributor[role='publisher']">
                  <xsl:value-of select="head/contributors/contributor[role='publisher']/link/@uri"/>
                </xsl:when>
              </xsl:choose>
              <xsl:value-of select="concat('/epub/', @xml:id)"/>
            </xsl:attribute>
          </meta>
	  <meta name="dtb:depth" content="1"/>
	  <meta name="dtb:totalPageCount" content="0"/>
	  <meta name="dtb:maxPageNumber" content="0"/>
	</head>
	<docTitle>
	  <text><xsl:apply-templates select="head/title" mode="text"/></text>
	</docTitle>
        <xsl:apply-templates select="head/contributors/contributor" mode="toc_ncx"/>
	<navMap>
          <xsl:if test=".//topic/@type='title'">
            <navPoint id="navPoint-1" playOrder="1">
              <navLabel><text>Page de titre</text></navLabel>
              <content src="{$fid}-tpc-1.html"/>
            </navPoint>
          </xsl:if>
          <xsl:if test="$toc">
            <navPoint id="navPoint-{count(.//topic[@type='title'])+1}"
                      playOrder="{count(.//topic[@type='title'])+1}">
              <navLabel><text>Sommaire</text></navLabel>
              <content src="{$fid}-toc.html"/>
            </navPoint>
          </xsl:if>
	  <xsl:apply-templates
              select="../topic|division|topic[not(@type='title')]"
              mode="toc_ncx"/>
	</navMap>
      </ncx>
    </xsl:document>  
  </xsl:template>

  <!--
      ========================================================================
      contributor mode toc_ncx
      ========================================================================
  -->
  <xsl:template match="contributor" mode="toc_ncx">
    <xsl:if test="not(role) or role='author'">
      <docAuthor>
        <text>
          <xsl:apply-templates select="firstname" mode="text"/>
          <xsl:text> </xsl:text>
          <xsl:if test="secondname">
            <xsl:apply-templates select="secondname" mode="text"/>
            <xsl:text> </xsl:text>
          </xsl:if>
          <xsl:apply-templates select="lastname" mode="text"/>
        </text>
      </docAuthor>
    </xsl:if>
  </xsl:template>
  
  <!--
      ========================================================================
      division mode toc_ncx
      ========================================================================
  -->
  <xsl:template match="division" mode="toc_ncx">
    <xsl:variable name="count">
      <xsl:value-of select="count(preceding::division)
                            + count(ancestor::division)
                            + count(preceding::topic)
                            + $toc + 1"/>
    </xsl:variable>
    <navPoint id="navPoint-{$count}" playOrder="{$count}">
      <navLabel>
        <text>
          <xsl:choose>
            <xsl:when test="head/title">
              <xsl:apply-templates select="head/title" mode="text"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:text>Division </xsl:text>
              <xsl:value-of select="count(preceding::division)+1"/>
            </xsl:otherwise>
          </xsl:choose>
          <xsl:if test="head/subtitle">
            <xsl:text> – </xsl:text>
            <xsl:apply-templates select="head/subtitle" mode="text"/>
          </xsl:if>
        </text>
      </navLabel>
      <xsl:choose>
        <xsl:when test="ancestor::division">
          <content src="{$fid}-tpc-{count(preceding::topic)+1}.html#div{count(ancestor::division)}"/>
        </xsl:when>
        <xsl:when test="$subtoc">
          <content src="{$fid}-div-{count(preceding::division)+1}.html"/>
        </xsl:when>
        <xsl:otherwise>
          <content src="{$fid}-tpc-{count(preceding::topic)+1}.html"/>
        </xsl:otherwise>
      </xsl:choose>
      <xsl:apply-templates select="division|topic" mode="toc_ncx"/>
    </navPoint>
  </xsl:template>

  <!--
      ========================================================================
      topic mode toc_ncx
      ========================================================================
  -->
  <xsl:template match="topic" mode="toc_ncx">
    <xsl:if test="not(@type='title')">
      <xsl:variable name="count">
        <xsl:value-of select="count(preceding::division)
                              + count(ancestor::division)
                              + count(preceding::topic)
                              + $toc + 1"/>
      </xsl:variable>
      <navPoint id="navPoint-{$count}" playOrder="{$count}">
        <navLabel>
          <text>
            <xsl:choose>
              <xsl:when test="head/title">
                <xsl:apply-templates select="head/title" mode="text"/>
              </xsl:when>
              <xsl:when test="parent::division/head/title">
                <xsl:apply-templates select="parent::division/head/title" mode="text"/>
              </xsl:when>
              <xsl:otherwise>
                <xsl:text>Page </xsl:text>
                <xsl:value-of select="count(preceding::topic)+1"/>
              </xsl:otherwise>
            </xsl:choose>
            <xsl:if test="head/subtitle">
              <xsl:text> – </xsl:text>
              <xsl:apply-templates select="head/subtitle" mode="text"/>
            </xsl:if>
          </text>
        </navLabel>
        <content src="{$fid}-tpc-{count(preceding::topic)+1}.html#top"/>
      </navPoint>
    </xsl:if>
  </xsl:template>
  
</xsl:stylesheet>
