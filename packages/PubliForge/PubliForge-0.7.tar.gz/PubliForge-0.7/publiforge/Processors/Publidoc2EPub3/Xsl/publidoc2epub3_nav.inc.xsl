<?xml version="1.0" encoding="UTF-8"?>
<!-- $Id: publidoc2epub3_nav.inc.xsl 3493e17111cc 2012/07/12 12:49:04 patrick $ -->
<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:date="http://exslt.org/dates-and-times"
                extension-element-prefixes="date">

  <!--
      =========================================================================
      Template nav_xhtml
      =========================================================================
  -->
  <xsl:template name="nav_xhtml">
    <xsl:document href="{$path}{$fid}-nav{$html_ext}" method="xml"
                  encoding="utf-8" indent="yes">
      <xsl:text disable-output-escaping="yes">&lt;!DOCTYPE html&gt;
</xsl:text>
      <xsl:comment> Generated by PubliForge, <xsl:value-of select="date:date-time()"/> </xsl:comment>
      <html xmlns="http://www.w3.org/1999/xhtml" xmlns:epub="http://www.idpf.org/2007/ops">
        <xsl:attribute name="xml:lang"><xsl:value-of select="$lang"/></xsl:attribute>
        <xsl:attribute name="lang"><xsl:value-of select="$lang"/></xsl:attribute>
        <head>
          <meta charset="utf-8"/>
          <title><xsl:value-of select="concat(head/title, ' – ', $i18n_toc)"/></title>
          <link rel="stylesheet" href="Css/reset.css" type="text/css"/>
          <link rel="stylesheet" href="Css/publidoc.css" type="text/css"/>
          <link rel="stylesheet" href="Css/custom.css" type="text/css"/>
        </head>
        <body class="pdocToc">
          <h1><xsl:apply-templates select="head/title"/></h1>
          <xsl:if test="subtitle">
            <h2><xsl:apply-templates select="subtitle"/></h2>
          </xsl:if>

          <nav epub:type="toc" id="toc">
            <h3><xsl:value-of select="$i18n_toc"/></h3>
            <ol>
              <xsl:apply-templates
                  select="../topic|../quiz|division|topic[not(@type='title')]|quiz"
                  mode="nav_toc"/>
            </ol>
          </nav>

          <nav epub:type="landmarks">
            <h3><xsl:value-of select="$i18n_guide"/></h3>
            <ol>
              <xsl:if test="head/cover or $cover">
                <li><a href="{$fid}-cover{$html_ext}" epub:type="cover">
                  <xsl:value-of select="$i18n_cover"/>
                </a></li>
              </xsl:if>
              <xsl:apply-templates select=".//topic[@type='title']" mode="nav_landmarks"/>
              <li><a href="#toc" epub:type="toc"><xsl:value-of select="$i18n_toc"/></a></li>
              <xsl:choose>
                <xsl:when test=".//topic">
                  <li><a href="{$fid}-tpc-1{$html_ext}" epub:type="bodymatter"><xsl:value-of select="$i18n_text"/></a></li>
                </xsl:when>
                <xsl:when test=".//quiz">
                  <li><a href="{$fid}-quz-1{$html_ext}" epub:type="bodymatter"><xsl:value-of select="$i18n_text"/></a></li>
                </xsl:when>
              </xsl:choose>
              <xsl:apply-templates select=".//topic[@type='copyright']" mode="nav_landmarks"/>
            </ol>
          </nav>
        </body>
      </html>
    </xsl:document>
  </xsl:template>

  <!--
      =========================================================================
      division mode nav_toc
      =========================================================================
  -->
  <xsl:template match="division" mode="nav_toc">
    <li>
      <a>
        <xsl:attribute name="href">
          <xsl:choose>
            <xsl:when test="$subtoc and not(ancestor::division)">
              <xsl:value-of select="concat($fid, '-div-', count(preceding::division)+1, $html_ext)"/>
            </xsl:when>
            <xsl:when test=".//quiz">
              <xsl:value-of select="concat($fid, '-quz-', count(preceding::quiz)+1, $html_ext)"/>
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="concat($fid, '-tpc-', count(preceding::topic)+1, $html_ext)"/>
            </xsl:otherwise>
          </xsl:choose>
        </xsl:attribute>
        <xsl:choose>
          <xsl:when test="head/title">
            <xsl:apply-templates select="head/title"/>
          </xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="concat($i18n_part, ' ', count(preceding::division)+1)"/>
          </xsl:otherwise>
        </xsl:choose>
        <xsl:if test="head/subtitle"> — </xsl:if>
        <xsl:apply-templates select="head/subtitle"/>
      </a>
      <ol>
        <xsl:apply-templates mode="nav_toc"/>
      </ol>
    </li>
  </xsl:template>

  <xsl:template match="head" mode="nav_toc"/>

  <!--
      =========================================================================
      topic mode nav_toc
      =========================================================================
  -->
  <xsl:template match="topic" mode="nav_toc">
    <li>
      <a href="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}">
        <xsl:choose>
          <xsl:when test="head/title">
            <xsl:apply-templates select="head/title"/>
          </xsl:when>
          <xsl:when test="section[1]/head/title">
            <xsl:apply-templates select="section[1]/head/title"/>
          </xsl:when>
          <xsl:when test="section[1]/p[1]/initial">
            <xsl:apply-templates select="section[1]/p[1]/initial" mode="toc"/>
          </xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="concat($i18n_chapter, ' ', count(preceding::topic)+1)"/>
          </xsl:otherwise>
        </xsl:choose>
        <xsl:if test="head/subtitle"> • </xsl:if>
        <xsl:apply-templates select="head/subtitle"/>
      </a>
      <xsl:if test="head/abstract">
        <div class="pdocAbstract">
          <xsl:apply-templates select="head/abstract"/>
        </div>
      </xsl:if>
    </li>
  </xsl:template>

  <!--
      =========================================================================
      quiz mode nav_toc
      =========================================================================
  -->
  <xsl:template match="quiz" mode="nav_toc">
    <li>
      <a href="{$fid}-quz-{count(preceding::quiz)+1}{$html_ext}">
        <xsl:choose>
          <xsl:when test="head/title">
            <xsl:apply-templates select="head/title"/>
          </xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="concat($i18n_quiz, ' ', count(preceding::quiz)+1)"/>
          </xsl:otherwise>
        </xsl:choose>
        <xsl:if test="head/subtitle"> • </xsl:if>
        <xsl:apply-templates select="head/subtitle"/>
      </a>
    </li>
  </xsl:template>

  <!--
      =========================================================================
      topic mode nav_landmarks
      =========================================================================
  -->
  <xsl:template match="topic" mode="nav_landmarks">
    <xsl:if test="@type='title' or @type='copyright'">
      <li>
        <a href="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}">
          <xsl:attribute xmlns:epub="http://www.idpf.org/2007/ops" name="epub:type">
            <xsl:choose>
              <xsl:when test="@type='title'">titlepage</xsl:when>
              <xsl:when test="@type='copyright'">copyright-page</xsl:when>
            </xsl:choose>
          </xsl:attribute>
          <xsl:choose>
            <xsl:when test="@type='title'"><xsl:value-of select="$i18n_title_page"/></xsl:when>
            <xsl:when test="@type='copyright'"><xsl:value-of select="$i18n_copyright"/></xsl:when>
          </xsl:choose>
        </a>
      </li>
    </xsl:if>
  </xsl:template>
  
</xsl:stylesheet>
