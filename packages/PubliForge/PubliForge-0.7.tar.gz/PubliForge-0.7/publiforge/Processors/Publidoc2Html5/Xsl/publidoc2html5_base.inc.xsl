<?xml version='1.0' encoding="utf-8"?>
<!-- $Id: publidoc2html5_base.inc.xsl 9a3c109c4e49 2012/08/08 16:52:55 patrick $ -->
<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:date="http://exslt.org/dates-and-times"
                extension-element-prefixes="date">

  <!--
      *************************************************************************
                                 CALLABLE TEMPLATES
      *************************************************************************
  -->
  <!--
      =========================================================================
      Template html_file
      =========================================================================
  -->
  <xsl:template name="html_file">
    <xsl:param name="name"/>
    <xsl:param name="title"/>
    <xsl:param name="body"/>
    <xsl:document href="{$path}{$name}{$html_ext}" method="xml"
                  omit-xml-declaration="yes" encoding="utf-8" indent="yes">
      <xsl:call-template name="html_frame">
        <xsl:with-param name="title" select="$title"/>
        <xsl:with-param name="body" select="$body"/>
      </xsl:call-template>
    </xsl:document>
  </xsl:template>

  <!--
      =========================================================================
      Template html_frame
      =========================================================================
  -->
  <xsl:template name="html_frame">
    <xsl:param name="title"/>
    <xsl:param name="body"/>
    <xsl:text disable-output-escaping="yes">&lt;!DOCTYPE html&gt;
</xsl:text>
    <xsl:comment> Generated by PubliForge, <xsl:value-of select="date:date-time()"/> </xsl:comment>
    <html xmlns="http://www.w3.org/1999/xhtml">
      <xsl:attribute name="xml:lang"><xsl:value-of select="$lang"/></xsl:attribute>
      <xsl:attribute name="lang"><xsl:value-of select="$lang"/></xsl:attribute>
      <head>
        <meta charset="utf-8"/>
        <title><xsl:value-of select="$title"/></title>
        <link rel="stylesheet" href="Css/reset.css" type="text/css"/>
        <link rel="stylesheet" href="Css/publidoc.css" type="text/css"/>
        <xsl:if test="name()='quiz'">
          <link rel="stylesheet" href="Css/publiquiz.css" type="text/css"/>
        </xsl:if>
        <link rel="stylesheet" href="Css/custom.css" type="text/css"/>
        <xsl:if test="$js and (name()='topic' or name()='quiz' or $onefile)">
          <script src="Js/mootools.js" type="text/javascript"><xsl:text> </xsl:text></script>
          <script src="Js/publidoc.js" type="text/javascript"><xsl:text> </xsl:text></script>
          <xsl:if test="name()='quiz'">
            <script src="Js/publiquiz.js" type="text/javascript"><xsl:text> </xsl:text></script>
          </xsl:if>
        </xsl:if>
      </head>
      <xsl:copy-of select="$body"/>
    </html>
  </xsl:template>

  <!--
      =========================================================================
      Template audio
      =========================================================================
  -->
  <xsl:template name="audio">
    <xsl:param name="id" select="@id"/>
    <xsl:param name="controls" select="1"/>
    <xsl:param name="autoplay" select="0"/>
    <audio id="aud-{$id}">
      <xsl:if test="$controls"><xsl:attribute name="controls"/></xsl:if>
      <xsl:if test="$autoplay"><xsl:attribute name="autoplay"/></xsl:if>
      <xsl:attribute name="class">
        <xsl:text>pdocAudio</xsl:text>
        <xsl:if test="name()='audio' and @type"> pdocAudio-<xsl:value-of select="@type"/></xsl:if>
      </xsl:attribute>
      <source src="{$aud_dir}{$id}{$aud_ext1}"/>
      <xsl:if test="$aud_ext1!=$aud_ext2">
        <source src="{$aud_dir}{$id}{$aud_ext2}"/>
      </xsl:if>
      <span><xsl:value-of select="$i18n_noaudio"/></span>
    </audio>
  </xsl:template>

  <!--
      =========================================================================
      Template video
      =========================================================================
  -->
  <xsl:template name="video">
    <xsl:param name="id" select="@id"/>
    <xsl:param name="controls" select="1"/>
    <xsl:param name="autoplay" select="0"/>
    <xsl:param name="width"/>
    <video id="vid-{$id}">
      <xsl:if test="$controls"><xsl:attribute name="controls"/></xsl:if>
      <xsl:if test="$autoplay"><xsl:attribute name="autoplay"/></xsl:if>
      <xsl:attribute name="class">
        <xsl:text>pdocVideo</xsl:text>
        <xsl:if test="name()='video' and @type"> pdocVideo-<xsl:value-of select="@type"/></xsl:if>
      </xsl:attribute>
      <xsl:if test="$width">
        <xsl:attribute name="width"><xsl:value-of select="$width"/></xsl:attribute>
      </xsl:if>
      <source src="{$vid_dir}{$id}{$vid_ext}"/>
      <!-- <xsl:if test="$vid_ext1!=$vid_ext2"> -->
      <!--   <source src="{$vid_dir}{$id}{$vid_ext2}"/> -->
      <!-- </xsl:if> -->
      <span><xsl:value-of select="$i18n_novideo"/></span>
    </video>
  </xsl:template>

</xsl:stylesheet>
